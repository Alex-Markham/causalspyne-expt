rule all:
    input:
        "results/boxplot.pdf",


pattern_dataset = (
    "method={method}/graph_size={graph_size}/seed={seed}/samp_size={samp_size}/"
)


rule generate_data:
    output:
        dataset=f"results/datasets/{pattern_dataset}data.csv",
        dag=f"results/datasets/{pattern_dataset}ground_truth_dag.csv",
        hidden_nodes=f"results/datasets/{pattern_dataset}hidden_nodes.csv",
    script:
        "scripts/generate_data.py"


rule fit_fci:
    input:
        dataset=rules.generate_data.output.dataset,
    output:
        pag=f"results/est/alg=fci/{pattern_dataset}pag.csv",
    script:
        "scripts/fit_fci.py"


rule fit_linlam:
    input:
        dataset=rules.generate_data.output.dataset,
    output:
        graph=f"results/est/alg=linlam/{pattern_dataset}graph.csv",
        causal_order=f"results/est/alg=linlam/{pattern_dataset}causal_order.csv",
    script:
        "scripts/fit_linlam.py"


rule evaluate_shd:
    input:
        true_dag=rules.generate_data.output.dag,
        hidden_nodes=rules.generate_data.output.hidden_nodes,
        est_pag=f"results/est/alg={{alg}}/{pattern_dataset}pag.csv",
    output:
        shd=f"results/eval/alg={{alg}}/{pattern_dataset}shd.csv",
    script:
        "scripts/eval_shd.py"


rule join_csv:
    input:
        expand(
            rules.evaluate_shd.output.shd,
            alg=["fci"],
            method=["standard", "child"],
            graph_size=["small", "medium"],
            seed=range(10),
            samp_size=[1000],
        ),
    output:
        csv="results/shd.csv",
    script:
        "scripts/join_csv.py"


rule boxplot:
    input:
        csv=rules.join_csv.output.csv,
    output:
        "results/boxplot.pdf",
    script:
        "scripts/boxplot.py"
